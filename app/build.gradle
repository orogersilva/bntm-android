apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

apply plugin: 'com.github.triplet.play'

// Versioning information
def versionMajor = 0
def versionMinor = 2
def versionPatch = 0

android {

    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {

        applicationId "com.orogersilva.bntm"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch * 1
        versionName "${versionMajor}.${versionMinor}.${versionPatch}-SNAPSHOT"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {

        release {

            storeFile rootProject.file('bntm.jks')
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("ALIAS_NAME")
            keyPassword System.getenv("ALIAS_PASSWORD")
        }
    }

    buildTypes {

        debug {

            buildConfigField "String", "NEON_API_URL",
                    "\"http://processoseletivoneon.azurewebsites.net/\""
        }

        release {

            signingConfig signingConfigs.release

            buildConfigField "String", "NEON_API_URL",
                    "\"http://processoseletivoneon.azurewebsites.net/\""

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {

        abortOnError false
    }

    testBuildType obtainTestBuildType()

    play {

        serviceAccountEmail = System.getenv("SERVICE_ACCOUNT_EMAIL")
        pk12File = rootProject.file('play.p12')
        track = 'alpha'
    }
}

// Show test status in console.
tasks.withType(Test) {

    testLogging {
        events "passed", "failed"
    }
}

def obtainTestBuildType() {

    def result = "debug"

    if (project.hasProperty("testBuildType")) {
        result = project.getProperties().get("testBuildType")
    }

    return result
}

configurations.all {

    resolutionStrategy {

        force "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
    }
}

dependencies {

    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"

    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }
    compile "com.github.BlacKCaT27:CurrencyEditText:$rootProject.currencyEditTextLibraryVersion"
    compile "com.google.code.gson:gson:$rootProject.gsonLibraryVersion"
    compile "com.googlecode.libphonenumber:libphonenumber:$rootProject.libPhoneNumberLibraryVersion"
    compile "com.jakewharton:butterknife:$rootProject.butterknifeLibraryVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterknifeLibraryVersion"
    compile "com.jakewharton.timber:timber:$rootProject.timberLibraryVersion"
    compile "com.mikhaellopez:circularimageview:$rootProject.circularImageViewLibraryVersion"
    compile "com.squareup.retrofit2:converter-gson:$rootProject.retrofitLibraryVersion"
    compile "com.squareup.retrofit2:retrofit:$rootProject.retrofitLibraryVersion"
    compile "com.wang.avi:library:$rootProject.avLoadingIndicatorViewLibraryVersion"

    testCompile "junit:junit:$rootProject.junitLibraryVersion"
    testCompile "org.mockito:mockito-core:$rootProject.mockitoLibraryVersion"

    androidTestCompile("com.android.support.test.espresso:espresso-core:$rootProject.espressoLibraryVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
